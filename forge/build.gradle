import net.minecraftforge.gradle.common.tasks.SignJar

buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.26", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file("../run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                editsign {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("../run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                editsign {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("../run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            args '--mod', 'editsign', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                editsign {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft(libs.forge)
    implementation project(":common")

    implementation(fg.deobf("me.shedaniel.cloth:cloth-config-forge:6.0.45"))
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from "src/main/resources", "../common/src/main/resources"

    filesMatching("META-INF/mods.toml") {
        expand project.properties
    }
}

jar.finalizedBy("reobfJar")

task signJar(type: SignJar, dependsOn: rootProject.buildJar, group: "modding") {
    onlyIf {
        project.hasProperty("forgeKeyStore")
                && project.hasProperty("forgeKeyStoreAlias")
                && project.hasProperty("forgeKeyStorePass")
                && project.hasProperty("forgeKeyStoreKeyPass")
                && rootProject.file(project.findProperty("forgeKeyStore")).exists()
    }

    keyStore = rootProject.file(project.findProperty("forgeKeyStore")).absolutePath
    alias = project.findProperty("forgeKeyStoreAlias")
    storePass = project.findProperty("forgeKeyStorePass")
    keyPass = project.findProperty("forgeKeyStoreKeyPass")
    inputFile = rootProject.jar.getArchiveFile()
    outputFile = rootProject.jar.getArchiveFile()
}
